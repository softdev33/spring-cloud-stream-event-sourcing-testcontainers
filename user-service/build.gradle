plugins {
    id 'org.springframework.boot' version '2.1.4.RELEASE'
    id 'io.spring.dependency-management' version '1.0.6.RELEASE'
    id 'com.palantir.docker' version '0.21.0'
}

group = 'com.mycompany'
version = '1.0.0'
sourceCompatibility = 1.8

ext {
    springCloudVersion = 'Greenwich.SR1'

    avroVersion = '1.8.2'
    modelMapperVersion = '2.3.2'
    springfoxSwaggerVersion = '2.9.2'
    braveVersion = '5.6.3'
    testcontainersVersion = '1.10.6'
    awaitilityVersion = '3.1.6'
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.cloud:spring-cloud-starter-zipkin')
    compile('org.springframework.cloud:spring-cloud-stream-schema')
    compile('org.springframework.cloud:spring-cloud-starter-stream-kafka')
    //-- Sleuth & Brave
    compile('org.springframework.cloud:spring-cloud-starter-sleuth')
    compile("io.zipkin.brave:brave:${braveVersion}")
    //--
    compile('org.springframework.kafka:spring-kafka')
    compile('com.google.code.gson:gson')
    compile("org.apache.avro:avro:${avroVersion}")
    compile("org.modelmapper:modelmapper:${modelMapperVersion}")
    compile("io.springfox:springfox-swagger2:${springfoxSwaggerVersion}")
    compile("io.springfox:springfox-swagger-ui:${springfoxSwaggerVersion}")
    compile('org.projectlombok:lombok')
    runtime('mysql:mysql-connector-java')
    testCompile("org.testcontainers:junit-jupiter:${testcontainersVersion}")
    testCompile("org.testcontainers:mysql:${testcontainersVersion}")
    testCompile("org.testcontainers:kafka:${testcontainersVersion}")
    testCompile("org.testcontainers:cassandra:${testcontainersVersion}")
    testCompile("org.awaitility:awaitility:${awaitilityVersion}")
    testCompile('org.springframework.boot:spring-boot-starter-test')
    //-- commented in order to send messages to kafka using testcontainers
    // testCompile('org.springframework.cloud:spring-cloud-stream-test-support')
    testCompile('org.junit.jupiter:junit-jupiter-api')
    testRuntime('org.junit.jupiter:junit-jupiter-engine')
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

// adding arguments to bootRun, for example: gradle user-service:bootRun -Dspring.profiles.active=test

bootRun {
    systemProperties = System.properties
}

// configuring docker builder plugin

docker {
    dependsOn build
    name "docker.mycompany.com/springboot-kafka-mysql-cassandra_user-service:${version}"
    files bootJar.archivePath
    buildArgs(['JAR_FILE': "${bootJar.archiveName}"])
}

// enable JUnit 5

test {
    useJUnitPlatform()
}
