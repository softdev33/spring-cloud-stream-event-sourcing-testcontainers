plugins {
    id 'org.springframework.boot' version '2.4.4'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'com.commercehub.gradle.plugin.avro' version '0.9.1'
    id 'com.google.cloud.tools.jib' version '2.8.0'
}

group = 'com.mycompany'
version = '1.0.0'
sourceCompatibility = 11

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

ext {
    set('springCloudVersion', '2020.0.2')
    set('avroVersion', '1.10.2')
    set('mapstructVersion', '1.4.2.Final')
    set('lombokMapstructBindingVersion', '0.2.0')
    set('springdocOpenApiVerison', '1.5.6')
    set('springGraalvmNativeVersion', '0.8.5')
    set('cassandraUnitSpringVersion', '4.3.1.0')
    set('openjdkImageVersion', '11.0.10')
    set('dockerImagePrefix', 'docker.mycompany.com')
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-cassandra'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.cloud:spring-cloud-stream'
    implementation 'org.springframework.cloud:spring-cloud-stream-binder-kafka'
    implementation 'org.springframework.kafka:spring-kafka'
    implementation 'org.springframework.cloud:spring-cloud-starter-bootstrap'
    implementation 'org.springframework.cloud:spring-cloud-starter-sleuth'
    implementation 'org.springframework.cloud:spring-cloud-sleuth-zipkin'
    implementation 'org.springframework.cloud:spring-cloud-schema-registry-client'

    implementation "org.apache.avro:avro:${avroVersion}"

    implementation "org.mapstruct:mapstruct:${mapstructVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
    testAnnotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

    // This dependency is needed so that Mapstruct can work with Lombok 1.18.16
    annotationProcessor "org.projectlombok:lombok-mapstruct-binding:${lombokMapstructBindingVersion}"

    implementation "org.springdoc:springdoc-openapi-ui:${springdocOpenApiVerison}"

    implementation "org.springframework.experimental:spring-graalvm-native:${springGraalvmNativeVersion}"

    compileOnly "org.projectlombok:lombok"
    annotationProcessor "org.projectlombok:lombok"

    testImplementation "org.cassandraunit:cassandra-unit-spring:${cassandraUnitSpringVersion}"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.kafka:spring-kafka-test'
}

test {
    useJUnitPlatform()
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

// configuring plugin for generating Java classes for Avro Schema 

avro {
    createSetters = false
    fieldVisibility = "PRIVATE"
}

task generateAvro(type: com.commercehub.gradle.plugin.avro.GenerateAvroJavaTask) {
    description = 'Generate Java classes from Avro schema located at src/main/resources/avro'
    source("src/main/resources/avro")
    outputDir = file("src/main/java/avro")
}

compileJava.source(generateAvro.outputs)

jib {
    from {
        image = "openjdk:${openjdkImageVersion}"
    }
    to {
        image ="${dockerImagePrefix}/${project.name}:${project.version}"
    }
    container {
        ports = ['8080']
    }
}

bootBuildImage {
    builder = "paketobuildpacks/builder:tiny"
    imageName = "${dockerImagePrefix}/${project.name}:${project.version}"
    environment = [
            "BP_BOOT_NATIVE_IMAGE" : "1",
            "BP_BOOT_NATIVE_IMAGE_BUILD_ARGUMENTS": "-Dspring.spel.ignore=true"
    ]
}
